ext.configureJavaCompilers = {
    int javaVersion ->
        def requiredVersion = JavaVersion.toVersion("1.${javaVersion}")

        sourceCompatibility = requiredVersion
        targetCompatibility = requiredVersion

        def compileTasks = tasks.withType(JavaCompile)
        def compilerOptions = compileTasks*.options
        compilerOptions*.encoding = 'UTF-8'
        compilerOptions*.compilerArgs = ['-Xlint:none']

        if (JavaVersion.current() != requiredVersion) {
            String jdkProperty = "JAVA${javaVersion}_HOME"
            String javaHome = System.getenv(jdkProperty)
            if (javaHome != null && !javaHome.isEmpty()) {
                String javac = javaHome + "/bin/javac.exe"
                compilerOptions*.fork = true
                compilerOptions*.forkOptions*.executable = javac.trim()
            } else {
                compileTasks*.doFirst {
                    logger.warn "Warning: ${jdkProperty} property is missing and not compiling with Java ${requiredVersion}. Using ${JavaVersion.current()}"
                }
            }
        }
}